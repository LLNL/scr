#!/usr/bin/perl -w
use strict;
use lib '@X_DATADIR@/scr';
use scr_param;
use Getopt::Long qw/ :config gnu_getopt ignore_case /;

# This script returns info on the SCR control and cache directories
# for the current user and jobid, it returns "INVALID" if something
# is not set.

# Better to have this directory construction in one place
# rather than having it duplicated over a number of different
# scripts

# TODO: read cache directory from config file
my $prog = "scr_cntl_dir";

my $bindir = "@X_BINDIR@";

my $param = undef;
if (defined $ENV{SCR_CONF_FILE}) {
  $param = new scr_param($ENV{SCR_CONF_FILE});
} else {
  $param = new scr_param();
}

sub print_usage
{
  print "\n";
  print "  Usage:  $prog [--base] [--cache] [--user <username>] [--jobid <id>]\n";
  print "\n";
  exit 1;
}

# read in command line arguments
my %conf = ();
$conf{base}  = undef;
$conf{cache} = undef;
$conf{user}  = undef;
$conf{jobid} = undef;
my $rc = GetOptions (
   "base|b"    => \$conf{base},
   "cache|c"   => \$conf{cache},
   "user|u=s"  => \$conf{user},
   "jobid|j=i" => \$conf{jobid},
);
if (not $rc) {
  print_usage();
}

# get the base directory
my @bases = ();
if (defined $conf{cache}) {
  # lookup cache base
  my $cachedesc = $param->get_hash("CACHEDESC");
  if (defined $cachedesc) {
    foreach my $index (keys %$cachedesc) {
      if (defined $$cachedesc{$index}{"BASE"}) {
        push @bases, (keys %{$$cachedesc{$index}{"BASE"}})[0];
      }
    }
  }
} else {
  # lookup cntl base
  push @bases, $param->get("SCR_CNTL_BASE");
}
if (@bases == 0) {
  print "INVALID\n";
  exit 1;
}

# get the user/job directory
my $suffix = undef;
if (not defined $conf{base}) {
  # if not specified, read username from environment
  if (not defined $conf{user}) {
    my $username = `$bindir/scr_env --user`;
    if ($? == 0) {
      chomp $username;
      $conf{user} = $username;
    }
  }

  # if not specified, read jobid from environment
  if (not defined $conf{jobid}) {
    my $jobid = `$bindir/scr_env --jobid`;
    if ($? == 0) {
      chomp $jobid;
      $conf{jobid} = $jobid;
    }
  }

  # check that the required environment variables are set
  if (not defined $conf{user} or
      not defined $conf{jobid})
  {
    # something is missnig, print invalid dir and exit with error
    print "INVALID\n";
    exit 1;
  }

  $suffix = "$conf{user}/scr.$conf{jobid}";
}

# ok, all values are here, print out the directory name and exit with success
my @dirs = ();
foreach my $base (@bases) {
  if (defined $suffix) {
    push @dirs, "$base/$suffix";
  } else {
    push @dirs, "$base";
  }
}
if (@dirs > 0) {
  print join(" ", @dirs), "\n";
}
exit 0;
